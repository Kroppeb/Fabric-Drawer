plugins {
    id 'fabric-loom'
    id 'maven-publish'
    id "org.jetbrains.kotlin.jvm"
    id "org.jetbrains.kotlin.plugin.serialization"
    id "com.jfrog.bintray"
    id 'com.matthewprenger.cursegradle'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

def total_version = "$mod_version-$minecraft_version"

archivesBaseName = project.archives_base_name
version = total_version
group = project.maven_group

minecraft {
}


def pomConfig = {
    licenses {
        license {
            name project.license
        }
    }
    developers {
        developer {
            id "fudge"
            name "natan"
            email "natandestroyer100@gmail.com"
        }
    }

    scm {
        url project.github_url
    }
}


repositories {
    maven { url = "http://maven.fabricmc.net/" }
    maven { url = "https://kotlin.bintray.com/kotlinx" }
    maven {url = "https://repo.spongepowered.org/maven"}
    maven { url= 'http://repo1.maven.org/maven2' }
    jcenter()
//    mavenCentral()
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:$minecraft_version"
    mappings "net.fabricmc:yarn:$yarn_mappings"
    modImplementation "net.fabricmc:fabric-loader:$loader_version"

    // Fabric API. This is technically optional, but you probably want it anyway.
    // modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation "net.fabricmc:fabric-language-kotlin:$fabric_kotlin_version"

    modApi "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$kotlinx_serialization_runtime_version"
    include "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$kotlinx_serialization_runtime_version"

    testImplementation('com.google.guava:guava:28.0-jre') {
        force = true
    }
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testImplementation 'org.spongepowered:lwts:1.0.0'
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"

    // PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
    // You may need to force-disable transitiveness on them.
}

processResources {
    inputs.property "version", total_version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": total_version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}


// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

curseforge {
    apiKey = project.hasProperty("curseforge_api_key") ? project.curseforge_api_key : ""
    project {
        id = '334410'
        releaseType = 'release'
        addGameVersion "Fabric"
        addGameVersion "1.14.4"
        changelogType = "markdown"
        changelog = file("changelog.md")

        mainArtifact(remapJar) {
            displayName = "Fabric Drawer $total_version"
        }
        addArtifact sourcesJar

        afterEvaluate {
            mainArtifact(remapJar)
            uploadTask.dependsOn(remapJar)
        }
    }

    options {
        forgeGradleIntegration = false
    }
}


// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}


jar {
    from "LICENSE"
}

bintray {
    user = project.hasProperty('bintray_user') ? project.property('bintray_user') : ""
    key = project.hasProperty('bintray_api_key') ? project.property('bintray_api_key') : ""
    publications = ["DrawerPublication"]
    publish = true //[Default: false] Whether version should be auto published after an upload
    pkg {
        repo = "libs"
        name = "fabric-drawer"
        desc = 'Fabric library mod for kotlin mods that allows you to easily save data to NBT and PacketByteBuf using kotlinx.serialization.'
        websiteUrl = project.github_url
        issueTrackerUrl = "${project.github_url}/issues"
        githubRepo = 'natanfudge/fabric-drawer' //Optional Github repository
        githubReleaseNotesFile = 'README.md' //Optional Github readme file
        licenses = [project.license]
        vcsUrl = project.github_url
        version {
            name = total_version
            released = new Date()
        }
    }

}


publishing {
    publications {
        DrawerPublication(MavenPublication) {
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(kotlinSourcesJar) {
                classifier = "sources"
                builtBy remapSourcesJar
            }
            groupId project.maven_group
            artifactId project.archives_base_name
            version total_version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Fabric mod that adds NBT and PacketByteBuf to kotlinx.serialization')
                root.appendNode('name', 'Fabric-Drawer')
                root.appendNode('url', project.github_url)
                root.children().last() + pomConfig
            }
        }
    }
}


test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}


Properties properties = new Properties()
File propertiesFile = new File('gradle.properties')
propertiesFile.withInputStream {
    properties.load(it)
}
properties.put("total_version",total_version)


task updateTemplate(type: Copy) {
    group = "template"
    description = "Switches out variables like \$total_version in .template.md files, and copies the results into .md files."
    from(rootDir)
    include("**/*.template.md")
    filesMatching("**/*.template.md") {
        def extensionLength = ".template.md".length()

        name = sourceName.substring(0, sourceName.length() - extensionLength) + ".md"
        expand(properties)
    }
    destinationDir = rootDir
}

bintrayUpload.dependsOn updateTemplate
bintrayUpload.dependsOn "curseforge334410"

