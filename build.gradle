plugins {
    id 'fabric-loom'
    id 'maven-publish'
    id "org.jetbrains.kotlin.jvm"
    id "org.jetbrains.kotlin.plugin.serialization"
    id "com.jfrog.bintray"
    id 'com.matthewprenger.cursegradle'
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

archivesBaseName = project.archives_base_name
version = project.mod_version
group = project.maven_group

minecraft {
}


def pomConfig = {
    licenses {
        license {
            name project.license
        }
    }
    developers {
        developer {
            id "fudge"
            name "natan"
            email "natandestroyer100@gmail.com"
        }
    }

    scm {
        url project.github_url
    }
}


repositories {
    maven { url = "http://maven.fabricmc.net/" }
    maven { url = "https://kotlin.bintray.com/kotlinx" }
    jcenter()
}

dependencies {
    //to change the versions see the gradle.properties file
    minecraft "com.mojang:minecraft:$minecraft_version"
    mappings "net.fabricmc:yarn:$yarn_mappings"
    modImplementation "net.fabricmc:fabric-loader:$loader_version"

    // Fabric API. This is technically optional, but you probably want it anyway.
    // modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

    modImplementation "net.fabricmc:fabric-language-kotlin:$fabric_kotlin_version"

    modApi "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$kotlinx_serialization_runtime_version"
    include "org.jetbrains.kotlinx:kotlinx-serialization-runtime:$kotlinx_serialization_runtime_version"

    testImplementation('com.google.guava:guava:28.0-jre') {
        force = true
    }
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"
    testImplementation "org.junit.jupiter:junit-jupiter-api:$junit_version"
    testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine:$junit_version"

    // PSA: Some older mods, compiled on Loom 0.2.1, might have outdated Maven POMs.
    // You may need to force-disable transitiveness on them.
}

processResources {
    inputs.property "version", project.mod_version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.mod_version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}




// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

curseforge {
    apiKey = project.hasProperty("curseforge_api_key") ? project.curseforge_api_key : ""
    project {
        id = '334410'
        changelog = 'First Release'
        releaseType = 'beta'
        addGameVersion "Fabric"
        changelogType = "markdown"
        changelog = file("changelog.md")

        mainArtifact(remapJar) {
            displayName = "Fabric Drawer $project.mod_version"
        }
        addArtifact sourcesJar

        afterEvaluate {
            mainArtifact(remapJar)
            uploadTask.dependsOn(remapJar)
        }
    }

    options {
        forgeGradleIntegration = false
    }
}



// ensure that the encoding is set to UTF-8, no matter what the system default is
// this fixes some edge cases with special characters not displaying correctly
// see http://yodaconditions.net/blog/fix-for-java-file-encoding-problems-with-gradle.html
tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}



jar {
    from "LICENSE"
}

bintray {
    user = project.hasProperty('bintray_user') ? project.property('bintray_user') : ""
    key = project.hasProperty('bintray_api_key') ? project.property('bintray_api_key') : ""
    publications = ["DrawerPublication"]
    publish = true //[Default: false] Whether version should be auto published after an upload
    pkg {
        repo = "libs"
        name = "fabric-drawer"
        desc = 'Fabric library mod for kotlin mods that allows you to easily save data to NBT and PacketByteBuf using kotlinx.serialization.'
        websiteUrl = project.github_url
        issueTrackerUrl = "${project.github_url}/issues"
        githubRepo = 'natanfudge/fabric-drawer' //Optional Github repository
        githubReleaseNotesFile = 'README.md' //Optional Github readme file
        licenses = [project.license]
        vcsUrl = project.github_url
        version {
            name = project.mod_version
            released = new Date()
        }
    }

}



publishing {
    publications {
        DrawerPublication(MavenPublication) {
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(kotlinSourcesJar) {
                classifier = "sources"
                builtBy remapSourcesJar
            }
            groupId project.maven_group
            artifactId project.archives_base_name
            version project.mod_version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Fabric mod that adds NBT and PacketByteBuf to kotlinx.serialization')
                root.appendNode('name', 'Fabric-Drawer')
                root.appendNode('url', project.github_url)
                root.children().last() + pomConfig
            }
        }
    }
}


test {
    useJUnitPlatform()
}

compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}

//String.metaClass.execute = {(command)-> "cmd /c $command" }
task updateTemplate {
    group = "template"
    description = "Parses the README.template.md file and inserts the current version into a new README.md file."
    doLast {
        if (!project.hasProperty("version")) throw new IllegalArgumentException("Please define a 'version' property in gradle.properties.")
        def readMeTemplateFile = new File("README.template.md")
        if (!readMeTemplateFile.exists()) throw new FileNotFoundException("could not find $readMeTemplateFile.")
        def readMeTemplateText = readMeTemplateFile.text
        def readMeFile = new File("README.md")
        readMeFile.write(readMeTemplateText.replace('$VERSION', project.version).replace('$KOTLIN_VERSION',project.kotlin_version))
    }
}

bintrayUpload.dependsOn updateTemplate
bintrayUpload.dependsOn "curseforge334410"

